OUTPUT_ARCH(rx)

istack_size         = 1024;
ustack_size         = 0;    /* Grows down -->so don't put a size constraint on it */
heap_size           = 10240;
jlink_debugram_size = 1024;

SECTIONS
{
	.fvectors 0xFFFFFF80 : AT (0xFFFFFF80)
	{
		KEEP(*(.fvectors))
	}
	.text 0xFFF80000 : AT (0xFFF80000)
	{
		*(.text)
		*(.text.*)
		*(P)
		etext = .;
	}
	.rvectors :
	{
		_rvectors_start = .;
		KEEP(*(.rvectors))
		_rvectors_end = .;
	}
	.init :
	{
		*(.init)
		__preinit_array_start = .;
		*(.preinit_array)
		__preinit_array_end = .;
		__init_array_start = (. + 3) & ~ 3;
		KEEP(*(.init_array))
		*(SORT(.init_array.*))
		__init_array_end = .;
		__fini_array_start = .;
		*(.fini_array)
		*(SORT(.fini_array.*))
		__fini_array_end = .;
	}
	.fini :
	{
		*(.fini)
	}
	.got :
	{
		*(.got)
		*(.got.plt)
	}
	.rodata :
	{
		*(.rodata)
		*(.rodata.*)
		*(C_1)
		*(C_2)
		*(C)
		_erodata = .;
	}
	.eh_frame_hdr :
	{
		*(.eh_frame_hdr)
	}
	.eh_frame :
	{
		*(.eh_frame)
	}
	.jcr :
	{
		*(.jcr)
	}
	.tors :
	{
		__CTOR_LIST__ = .;
		___ctors = .;
		*(.ctors)
		___ctors_end = .;
		__CTOR_END__ = .;
		__DTOR_LIST__ = .;
		___dtors = .;
		*(.dtors)
		___dtors_end = .;
		__DTOR_END__ = .;
		_mdata = .;
	}                  
    .debugger_ram 0x00017BFC : AT (0x00017BFC)
    {
        _debugger_ram_start = .;
        . = . + jlink_debugram_size;
        _debugger_ram_end = .;
    }
	.istack 0x00017BFC : AT (0x00017BFC)
	{
		_istack = .;
	}
	.ustack 0x000177FC : AT (0x000177FC)
	{
        _ustack = .;
	}                
	. = ALIGN(4);
	.data 0x00000004: AT (_mdata)
	{
		_data = .;
		*(.data)
		*(.data.*)
		*(D)
		*(D_1)
		*(D_2)
		_edata = .;
	}
	.gcc_exc :
	{
		*(.gcc_exc)
	}
	.bss :
	{
		_bss = .;
		*(.bss)
		*(.bss.*)
		*(COMMON)
		*(B)
		*(B_1)
		*(B_2)
		_ebss = .;
		_end = .;
	}
	. = ALIGN(4);
    .heap :
    {
        _heap_start = .;
        *(.heap)
        . = . + heap_size;
        _heap_end = .;
    }

}
